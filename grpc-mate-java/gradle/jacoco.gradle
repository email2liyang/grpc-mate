apply plugin: "jacoco"

jacoco {
    toolVersion = "0.7.9"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                     exclude: ['**/generated/**',
                               'com/google/**',
                               'io/datanerd/es/server/**',
                               'io/datanerd/es/metrics/AutoValue**',
                               'io/datanerd/hello/server/**',])
        })
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'PACKAGE'
            excludes = ['com.google.*',
                        'io.datanerd.generated*',
                        'io.datanerd.es.dao*',
                        'io.datanerd.es.server*',
                        'io.datanerd.es.metrics*',
                        'io.datanerd.hello.server*',]
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
        rule {
            element = 'PACKAGE'
            includes = ['io.datanerd.es.metrics*']
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }
        }
    }
}
//make sure jacoco related task run after test
test.finalizedBy(jacocoTestReport, jacocoTestCoverageVerification)
