buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        // ASSUMES GRADLE 2.12 OR HIGHER. Use plugin version 0.7.5 with earlier
        // gradle versions
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.0'
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.10"
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.8.1'
}

apply plugin: 'idea'
apply plugin: 'com.google.protobuf'
apply plugin: 'application'
apply plugin: 'distribution'
apply plugin: "net.ltgt.apt"

repositories {
    mavenCentral()
}

mainClassName = "io.datanerd.hello.server.ServiceLauncher"
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.3.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.6.1'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                outputSubDir = 'java'
            }
        }
    }
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
    }
}

dependencies {
    // DI
    compile 'com.google.inject:guice:4.1.0'

    // Utils
    compile 'com.google.guava:guava:22.0'
    compile 'commons-beanutils:commons-beanutils:1.9.3'


    // logging bridges
    compile 'org.slf4j:jul-to-slf4j:1.7.25'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'org.slf4j:jcl-over-slf4j:1.7.25'
    compile 'org.slf4j:log4j-over-slf4j:1.7.25'

    // gRPC
    compile 'io.grpc:grpc-netty:1.6.1'
    compile 'io.grpc:grpc-protobuf:1.6.1'
    compile 'io.grpc:grpc-stub:1.6.1'

    // test dependencies
    testCompile 'io.grpc:grpc-testing:1.6.1'
    testCompile 'junit:junit:4.12'
    testCompile 'com.github.javafaker:javafaker:0.13'
    testCompile 'org.mockito:mockito-core:2.8.47'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.testcontainers:testcontainers:1.3.1'
}

test {
    testLogging {
        // set options for log level LIFECYCLE
        events "failed"
        exceptionFormat "full"

        // remove standard output/error logging from --info builds
        // by assigning only 'failed' and 'skipped' events
        info.events = ["failed", "skipped"]
    }
}